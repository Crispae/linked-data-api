#+ summary: Returns the chemicals related to a specific adverse outcome.
#+ endpoint: https://aopwiki.rdf.bigcat-bioinformatics.org/sparql/
#+ tags:
#+   - AOP
#+ defaults:
#+   - AOname: fibrosis

PREFIX aopo: <http://aopkb.org/aop_ontology#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX nci: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>

SELECT ?AO ?AOname ?AOP (CONCAT('"', GROUP_CONCAT(?ChemicalName; SEPARATOR='","'), '"') AS ?ChemicalNames) WHERE {
  ?AO a aopo:KeyEvent ; 
      dc:identifier ?AOLookup ; 
      dc:title ?AOname . 
  ?AOP a aopo:AdverseOutcomePathway ; 
       aopo:has_adverse_outcome ?AOLookup ;
       nci:C54571 ?Stressor .
  ?Stressor dc:title ?StressorName.
  OPTIONAL {
    ?Stressor aopo:has_chemical_entity ?Chemical. 
    ?Chemical dc:title ?ChemicalName.
  }
  FILTER regex(?AOname, ?__AOname, "i")
} 
ORDER BY DESC(?AO)
